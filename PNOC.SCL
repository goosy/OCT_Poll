FUNCTION_BLOCK "PNOC"
TITLE='PN Open Comm'
VERSION:'1.00'
AUTHOR:Goosy
NAME:PNOC
FAMILY:IE

VAR_INPUT 
    TCON_Parameters : STRUCT     
        block_length : WORD  := W#16#40;    //#!TCON config!#
        id : WORD  := W#16#1;    
        connection_type : BYTE  := B#16#11;    
        active_est : BOOL := TRUE;    
        local_device_id : BYTE  := B#16#2;    
        local_tsap_id_len : BYTE ;    
        rem_subnet_id_len : BYTE ;    
        rem_staddr_len : BYTE  := B#16#4;    
        rem_tsap_id_len : BYTE ;    
        next_staddr_len : BYTE ;    
        local_tsap_id : ARRAY  [1 .. 16 ] OF BYTE  := B#16#0;    
        rem_subnet_id : ARRAY  [1 .. 6 ] OF BYTE  := B#16#0;    
        rem_staddr : ARRAY  [1 .. 6 ] OF BYTE  := B#16#C0, B#16#A8, B#16#15, B#16#A;    
        rem_tsap_id : ARRAY  [1 .. 16 ] OF BYTE  := B#16#1, B#16#F6;    
        next_staddr : ARRAY  [1 .. 6 ] OF BYTE  := B#16#0;    
        spare : WORD ;    //#!TCON config!#
    END_STRUCT ; 
    customTrigger : BOOL ; //默认为false, 即以transPeriod周期发送并随时接收。当本参数为true时，由 conTrigger sendTrigger recvTrigger disconTrigger 定义收发方式。
    transPeriod : TIME := T#500MS; // 发送周期
    sendP : ANY;
    recvP : ANY;
    sendLength: INT;
END_VAR

VAR_IN_OUT
    conTrigger : BOOL;
    sendTrigger : BOOL;
    recvTrigger : BOOL;
    disconTrigger: BOOL;
END_VAR

VAR_OUTPUT
END_VAR

VAR
    TCON_ID : TCON;
    TSEND_ID : TSEND;
    TRCV_ID : TRCV;
    TDISCON_ID : TDISCON;
    TimeOver : TON;
END_VAR

VAR_TEMP
END_VAR

BEGIN
    TCON_ID (
        REQ                      := conTrigger,
        ID                       := TCON_Parameters.id,
        CONNECT                  := TCON_Parameters);
    
    TSEND_ID (
        REQ                      := sendTrigger,
        ID                       := TCON_Parameters.id,
        LEN                      := sendLength,
        DATA                     := sendP);
    
    TRCV_ID (
        EN_R                     := recvTrigger,
        ID                       := TCON_Parameters.id,
        DATA                     := recvP);
    
    TDISCON_ID (
        REQ                      := disconTrigger,
        ID                       := TCON_Parameters.id);

    // 当前发送计时
    TimeOver(
        IN := sendTrigger,
        PT := transPeriod);
    IF NOT customTrigger AND TimeOver.Q THEN
        sendTrigger := FALSE;
        conTrigger := TRUE;
        recvTrigger := TRUE;
    ELSIF NOT customTrigger THEN
        sendTrigger := TRUE; 
        conTrigger := FALSE;  
        disconTrigger := FALSE;
    END_IF;    

END_FUNCTION_BLOCK

